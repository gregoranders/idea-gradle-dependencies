plugins {
  id 'java'
  id 'java-library'
  id 'groovy'
  id 'idea'
  id 'checkstyle'
  id 'pmd'
  id 'jacoco'
  id 'maven-publish'
  id 'signing'
  id 'project-report'
  id 'build-dashboard'
  id 'com.github.spotbugs' version '5.0.4'
  id 'org.owasp.dependencycheck' version '6.5.2.1'
  id 'org.jetbrains.intellij' version '1.3.0'
  id 'org.sonarqube' version '3.3'
  id 'projectConfiguration'
}

def javaVersion = JavaVersion.VERSION_11

repositories {
  mavenCentral()
  maven {
    url 'https://cache-redirector.jetbrains.com/intellij-dependencies'
  }
  mavenLocal()
}

java {
  sourceCompatibility = javaVersion
  targetCompatibility = javaVersion
}

idea {
  module {
    downloadJavadoc = true
    downloadSources = true
  }

  project {
    vcs = 'Git'
    jdkName = "jdk-${javaVersion}"
    languageLevel = javaVersion
  }
}

jar {
  def manifestAttributes = [
    'Created-By'              : "Gradle ${gradle.gradleVersion}",
    'Specification-Title'     : "${project.property('description')}",
    'Specification-Version'   : project.version,
    'Specification-Vendor'    : "${rootProject.property('author')} <${project.property('email')}>",
    'Implementation-Title'    : project.property('description'),
    'Implementation-Version'  : project.version,
    'Implementation-Vendor'   : "${rootProject.property('author')} <${project.property('email')}>",
    'Implementation-Vendor-Id': "${rootProject.property('author')} <${project.property('email')}>",
  ]

  manifest {
    attributes(manifestAttributes)
  }
}

test {
  systemProperty 'com.athaydes.spockframework.report.projectName', "${project.group}.${project.name}"
  systemProperty 'com.athaydes.spockframework.report.projectVersion', "${project.version}"
}

intellij {
  updateSinceUntilBuild = false
  pluginName = property('pluginName')
  version = property('pluginPlatformVersion')
  type = property('pluginPlatformType')
  plugins = ['java', 'gradle', 'Groovy']
}

dependencies {

  annotationProcessor(
    [group: 'org.immutables', name: 'value', version: property('immutablesVersion')]
  )

  implementation(
    [group: 'javax.inject', name: 'javax.inject', version: property('injectVersion')],
    [group: 'org.immutables', name: 'value-annotations', version: property('immutablesVersion')],
  )

  testImplementation(
    [group: 'org.codehaus.groovy', name: 'groovy-all', version: property('groovyVersion')],
    [group: 'io.github.joke', name: 'spock-mockable', version: property('spockMockableVersion')],
  )

  testImplementation(
    [group: 'org.spockframework', name: 'spock-core', version: property('spockVersion')],
  ) {
    exclude group: 'org.codehaus.groovy'
  }

  testRuntimeOnly(
    [group: 'com.athaydes', name: 'spock-reports', version: property('spockReportsVersion')]
  ) {
    transitive = false
  }
}
